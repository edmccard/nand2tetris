// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Mux16(a=do, b=io, sel=inc, out=m1);
    Mux16(a=m1, b=in, sel=load, out=m2);
    Mux16(a=m2, b=false, sel=reset, out=di);
    Inc16(in=do, out=io);
    Or(a=load, b=inc, out=l1);
    Or(a=l1, b=reset, out=l2);
    Register(in=di, out=out, out=do, load=l2);
}
